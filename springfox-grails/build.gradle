/*
 * Copyright 2017 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
plugins {
    id "io.spring.dependency-management" version "1.0.11.RELEASE"
    id 'jacoco'
}

apply plugin: 'groovy'
apply plugin: 'idea'
apply from: 'gradle/publishing.gradle'

description = 'Springfox Grails module that produces grails specific documentation'

repositories {
    maven { url "https://repo.grails.org/grails/core" }
    maven { url "https://plugins.gradle.org/m2/" }
    mavenCentral()
}

configurations {
    provided
    compile.extendsFrom provided
}

dependencyManagement {
    imports {
        mavenBom "org.grails:grails-bom:$grailsVersion"
    }
    dependencies {
        dependency('com.jayway.jsonpath:json-path:2.4.0')
        dependency('org.slf4j:slf4j-simple:1.7.22')
        dependency('org.yaml:snakeyaml:1.19')
        dependency('io.swagger:swagger-annotations:1.5.20')
        dependency('org.springframework.boot:spring-boot-autoconfigure:1.5.15.RELEASE')
        dependency('javax.servlet:javax.servlet-api:4.0.1')
        dependency('com.google.guava:guava:20.0')
        dependency('com.fasterxml:classmate:1.4.0')
        dependency('org.objenesis:objenesis:3.2')
        dependency("io.springfox:springfox-spi:$springfox")
        dependency("io.springfox:springfox-core:$springfox")
        dependency("io.springfox:springfox-swagger2:$springfox")
    }
    applyMavenExclusions false
}

dependencies {
    testCompile "org.springframework:spring-test"
    testCompile "com.jayway.jsonpath:json-path"
    testCompile "org.slf4j:slf4j-simple"
    testCompile "org.yaml:snakeyaml"
    testCompile "org.spockframework:spock-spring"
    testCompile "org.spockframework:spock-core"
    testCompile "org.grails:grails-plugin-rest"
    testCompile "org.objenesis:objenesis"

    provided "io.swagger:swagger-annotations"
    provided "org.springframework:spring-core"
    provided "org.springframework:spring-web"
    provided "org.springframework:spring-webmvc"
    provided "org.springframework:spring-context"
    provided "org.springframework.boot:spring-boot-autoconfigure"
    provided "org.grails:grails-core"
    provided "org.grails:grails-web-url-mappings"
    provided "javax.servlet:javax.servlet-api"

    compile "com.google.guava:guava"
    compile "com.fasterxml:classmate"
    compile "org.slf4j:slf4j-api"
    compile "io.springfox:springfox-spi"
    compile "io.springfox:springfox-core"
    compile "io.springfox:springfox-swagger2"
    compile "net.bytebuddy:byte-buddy"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    getArchiveClassifier().set('sources')
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    getArchiveClassifier().set('javadoc')
    from javadoc.destinationDir
}

jar {
    manifest {
        attributes(
            'Built-By': 'Dilip Krishnan',
            'Created-By': System.properties['java.version'] + " (" + System.properties[
                'java.vendor'] + " " + System.properties['java.vm.version'] + ")",
            'Build-Date': project.buildDate,
            'Build-Time': project.buildTime,
            'Specification-Title': project.name,
            'Specification-Version': project.version,
            'Implementation-Title': project.name,
            'Implementation-Version': project.version,
            "SpringFox-Version": "$project.springfox",
            )
    }
}


jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled true
    }
}



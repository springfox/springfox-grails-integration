/*
 * Copyright 2017 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
buildscript {
  repositories {
    maven {
      url "https://plugins.gradle.org/m2/"
    }
    jcenter()
    mavenCentral()
  }

}

plugins {
  id "com.jfrog.bintray" version "1.8.4"
  id "com.jfrog.artifactory" version "4.24.12"
}

apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'jacoco'
apply from: 'gradle/publishing.gradle'

description = 'Springfox Grails module that produces grails specific documentation'

configurations {
    provided
    compile.extendsFrom provided
}

dependencies {
    testCompile "org.springframework:spring-test:${spring}"
    testCompile "com.jayway.jsonpath:json-path:${jsonPath}"
    testCompile "org.slf4j:slf4j-simple:${slf4j}"
    testCompile "org.yaml:snakeyaml:${snakeyaml}"
    testCompile "org.spockframework:spock-spring:${spock}"
    testCompile "org.spockframework:spock-core:${spock}"
    testCompile "org.codehaus.groovy:groovy-all:${groovy}"
    testCompile "org.grails:grails-plugin-testing:${grails}"
    testCompile "org.grails:grails-plugin-rest:${grails}"

    provided "io.swagger:swagger-annotations:${swagger2Core}"
    provided "org.springframework:spring-core:$spring"
    provided "org.springframework:spring-web:$spring"
    provided "org.springframework:spring-webmvc:$spring"
    provided "org.springframework:spring-context:$spring"
    provided "org.springframework.boot:spring-boot-autoconfigure:$springBoot"
    provided "org.grails:grails-core:$grails"
    provided "org.grails:grails-web-url-mappings:$grails"
    provided "org.grails:grails-web-url-mappings:$grails"
    provided "javax.servlet:javax.servlet-api:$servlet"

    compile "com.google.guava:guava:${guava}"
    compile "com.fasterxml:classmate:${classmate}"
    compile "org.slf4j:slf4j-api:${slf4j}"
    compile "io.springfox:springfox-spi:${springfox}"
    compile "io.springfox:springfox-core:${springfox}"
    compile "io.springfox:springfox-swagger2:${springfox}"
    compile "net.bytebuddy:byte-buddy:${byteBuddyVersion}"
}

task sourcesJar(type: Jar, dependsOn: classes) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

jar {
    manifest {
      attributes(
          'Built-By': 'Dilip Krishnan',
          'Created-By': System.properties['java.version'] + " (" + System.properties['java.vendor'] + " " + System.properties['java.vm.version'] + ")",
          'Build-Date': project.buildDate,
          'Build-Time': project.buildTime,
          'Specification-Title': project.name,
          'Specification-Version': project.version,
          'Implementation-Title': project.name,
          'Implementation-Version': project.version,
          "SpringFox-Version": "$project.springfox",
      )
    }
}

jacoco {
  toolVersion = "$jacocoVersion"
}

jacocoTestReport {
  reports {
    xml.enabled true
    html.enabled true
  }
}

test {
  jvmArgs "-Dorg.spockframework.mock.ignoreByteBuddy=true"
}


